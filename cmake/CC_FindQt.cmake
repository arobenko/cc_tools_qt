macro (cc_find_qt_major)
    while (TRUE)
        if (CC_TOOLS_QT_USE_QT5)
            find_package(Qt5 REQUIRED COMPONENTS Core Widgets)
            find_package(QT NAMES Qt5 REQUIRED COMPONENTS Core)
            break()
        endif ()

        find_package(Qt6 COMPONENTS Core Widgets)
        if (Qt6_FOUND)
            find_package(QT NAMES Qt6 REQUIRED COMPONENTS Core)
            break ()
        endif ()

        find_package(Qt5 COMPONENTS Core Widgets REQUIRED)
        find_package(QT NAMES Qt5 REQUIRED COMPONENTS Core)
        break()
    endwhile ()

    message (STATUS "QT_VERSION_MAJOR=${QT_VERSION_MAJOR}")

    if (NOT TARGET Qt::Core)
        add_library(Qt::Core ALIAS Qt${QT_VERSION_MAJOR}::Core)
    endif ()

    if (NOT TARGET Qt::Widgets)
        add_library(Qt::Core ALIAS Qt${QT_VERSION_MAJOR}::Widgets)
    endif ()    
endmacro ()

macro (cc_find_qt_components)
    while (TRUE)
        if (CC_TOOLS_QT_USE_QT5)
            find_package(Qt5 OPTIONAL_COMPONENTS ${ARGN})
            break()
        endif ()

        find_package(Qt6 OPTIONAL_COMPONENTS ${ARGN})
        if (Qt6_FOUND)
            break ()
        endif ()

        find_package(Qt5 OPTIONAL_COMPONENTS ${ARGN})
        break()
    endwhile ()

    foreach (c ${ARGN})
        if (NOT TARGET Qt${QT_VERSION_MAJOR}::${c})
            continue ()
        endif ()

        if (NOT TARGET Qt::${c})
            add_library(Qt::${c} ALIAS Qt${QT_VERSION_MAJOR}::${c})
        endif ()        
    endforeach ()
endmacro ()

macro (cc_qt_wrap_cpp _name)
    if ("${QT_VERSION_MAJOR}" STREQUAL "5")
        qt5_wrap_cpp(${_name} ${ARGN})
    else ()
        qt_wrap_cpp(${_name} ${ARGN})
    endif ()
endmacro ()

macro (cc_qt_wrap_ui _name)
    if ("${QT_VERSION_MAJOR}" STREQUAL "5")
        qt5_wrap_ui(${_name} ${ARGN})
    else ()
        qt_wrap_ui(${_name} ${ARGN})
    endif ()
endmacro ()

macro (cc_qt_add_resources _name)
    if ("${QT_VERSION_MAJOR}" STREQUAL "5")
        qt5_add_resources(${_name} ${ARGN})
    else ()
        qt_add_resources(${_name} ${ARGN})
    endif ()
endmacro ()

